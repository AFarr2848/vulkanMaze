cmake_minimum_required(VERSION 3.10)
project(VulkanTest LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

add_executable(VulkanTest
  src/Buffers.cpp
  src/Descriptors.cpp
  src/FrameData.cpp
  src/Pipelines.cpp
  src/Images.cpp
  src/Swapchain.cpp
  src/VulkanContext.cpp
  src/VulkanEngine.cpp
  src/Window.cpp
  src/main.cpp
  src/Mesh.cpp
  src/Material.cpp
)

find_package(Vulkan REQUIRED)
find_package(PkgConfig REQUIRED)
find_package(assimp REQUIRED)


pkg_check_modules(GLFW REQUIRED glfw3)

target_compile_definitions(VulkanTest PRIVATE
    VULKAN_HPP_NO_STRUCT_CONSTRUCTORS
)

target_include_directories(VulkanTest PRIVATE
    ${Vulkan_INCLUDE_DIRS}
    ${GLFW_INCLUDE_DIRS}
)

target_include_directories(VulkanTest
  PUBLIC
      ${CMAKE_CURRENT_SOURCE_DIR}/include

)

target_precompile_headers(VulkanTest PRIVATE
    ${CMAKE_SOURCE_DIR}/include/pch.hpp
)

target_link_libraries(VulkanTest
    ${Vulkan_LIBRARIES}
    ${GLFW_LIBRARIES}
    dl
    pthread
    assimp
)

# ---- Shader compilation section ----

# Path to slang compiler (must be installed or built)
set(SLANGC_EXECUTABLE slangc)

# Input/output locations
set(SHADER_DIR ${CMAKE_SOURCE_DIR}/src/shaders)
set(SHADER_OUT_DIR ${CMAKE_BINARY_DIR}/shaders)
file(MAKE_DIRECTORY ${SHADER_OUT_DIR})

# List of .slang shaders
set(SHADER_SOURCES
    ${SHADER_DIR}/shader.slang
)

set(SHADER_BINARIES "")

foreach(SHADER ${SHADER_SOURCES})
  get_filename_component(SHADER_NAME ${SHADER} NAME_WE)
  set(OUTPUT_FILE ${SHADER_OUT_DIR}/${SHADER_NAME}.spv)

  add_custom_command(
        OUTPUT ${OUTPUT_FILE}
        COMMAND ${SLANGC_EXECUTABLE}
                ${SHADER}
                -target spirv
                -profile spirv_1_4
                -emit-spirv-directly
                -fvk-use-entrypoint-name
                -entry vertMain
                -entry fragMain
                -o ${OUTPUT_FILE}
        DEPENDS ${SHADER}
        COMMENT "Compiling ${SHADER_NAME}.slang â†’ SPIR-V"
        VERBATIM
    )

  list(APPEND SHADER_BINARIES ${OUTPUT_FILE})
endforeach()

add_custom_target(compile_shaders ALL DEPENDS ${SHADER_BINARIES})
add_dependencies(VulkanTest compile_shaders)
